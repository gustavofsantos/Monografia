\chapter{Contrato de UsuÃ¡rio}\label{anexo:contrato-usuario}

\begin{lstlisting}
pragma solidity 0.4.24;

contract User {
  address owner;
  address lastContract;
  address nextContract;
  address originalContract;

  string  cid;
  string  publicKey;
  bool    enable;

  mapping (string => bool) signed;
  mapping (string => bool) secureDevicesPublicKeys;
  mapping (address => bool) authorities;

  // MODIFIERS
  modifier onlyUser(address caller) {
    require(
      caller == owner,
      "Only the user that own this contract can call this"
    );
    _;
  }

  modifier onlyAuthority(address caller) {
    require(
      authorities[caller] == true,
      "Only the authority that has registered this contract can call this"
    );
    _;
  }

  modifier onlyIfNotRegisteredAt(address authority) {
    require (
      authorities[authority] != true,
      "The contract is already registered at this authority"
    );
    _;
  }

  modifier onlyIfRegisteredAt(address authority) {
    require (
      authorities[authority] == true,
      "The contract need to be registered at this authority"
    );
    _;
  }

  modifier onlyEnabled() {
    require(
      enable == true,
      "The contract need to be enabled"
    );
    _;
  }

  modifier onlyDeviceNotSetted(string devicePublicKey) {
    require(
      secureDevicesPublicKeys[devicePublicKey] != true,
      "Secure device public key already exists"
    );
    _;
  }

  modifier onlyDeviceSetted(string devicePublicKey) {
    require(
      secureDevicesPublicKeys[devicePublicKey] == true,
      "Secure device public key not exists"
    );
    _;
  }

  modifier onlyNotSigned(string cidDataSigned) {
    require(
      signed[cidDataSigned] != true,
      "Data is already signed"
    );
    _;
  }

  constructor (string userCID, string userPublicKey, address userOriginalContract, address userLatestContract) public {
    owner = msg.sender;
    cid = userCID;
    publicKey = userPublicKey;
    originalContract = userOriginalContract;
    lastContract = userLatestContract;

    // setting data
    enable = true;
    nextContract = address(0);
  }

  function registerToAuthority(address authorityAddress) public
    onlyUser(msg.sender)
    onlyEnabled()
    onlyIfNotRegisteredAt(authorityAddress)
  {
    authorities[authorityAddress] = true;
  }

  function unregisterFromAuthority(address authorityAddress) public
    onlyUser(msg.sender)
    onlyEnabled()
    onlyIfRegisteredAt(authorityAddress)
  {
    authorities[authorityAddress] = false;
  }

  function isRegisteredByAuthority(address authority) public view returns (bool) {
    return authorities[authority];
  }

  function setSecureDevicePublicKey(string devicePublicKey) public 
    onlyUser(msg.sender)
    onlyEnabled()
    onlyDeviceNotSetted(devicePublicKey)
  {
    secureDevicesPublicKeys[devicePublicKey] = true;
  }

  function disableSecureDevice(string devicePublicKey) public 
    onlyUser(msg.sender)
    onlyEnabled()
    onlyDeviceSetted(devicePublicKey)
  {
    secureDevicesPublicKeys[devicePublicKey] = false;
  }

  function disableAndLinkToNew(address newUserContract) public 
    onlyAuthority(msg.sender)
    onlyEnabled()
  {
    // disable the contract
    enable = false;
    // then set the new contract address
    nextContract = newUserContract;
  }

  function signData(string cidDataSigned) public
    onlyUser(msg.sender)
    onlyEnabled()
    onlyNotSigned(cidDataSigned)
  {
    signed[cidDataSigned] = true;
  }

  function isSigned(string cidDataSigned) public view returns (bool) {
    return signed[cidDataSigned];
  }

  function getPublicKey() public view returns (string) {
    return publicKey;
  }

  function getCID() public view returns (string) {
    return cid;
  }

  function getNextContract() public view returns (address) {
    return nextContract;
  }

  function getLastContract() public view returns (address) {
    return lastContract;
  }

  function getOriginalContract() public view returns (address) {
    return originalContract;
  }

  function getOwner() public view returns (address) {
    return owner;
  }
}
\end{lstlisting}